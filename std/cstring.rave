/*
This Source Code Form is subject to the terms of the Mozilla
Public License, v. 2.0. If a copy of the MPL was not distributed
with this file, You can obtain one at http://mozilla.org/MPL/2.0/.
*/

import <std/thread>

namespace libc {
    extern(vararg, linkname: "sprintf") int cformat(char* str, char* fmt);
    extern(linkname: "strtok") char* strtok(char* str, char* chars);
    extern(linkname: "strtol") long strtol(char* str, char** end, int base);
}

namespace std {
    namespace cstring {
        int strlen(const(char)* s) {
            while(s[return] != '\0') return += 1;
        }

        (inline) const(char)* fromBool(bool b) {
            return = "false";
            if(b) return = "true";
        }

        (inline) bool strncmp(const(char)* s1, const(char)* s2, int n) {
            int i = 0;
            return = true;
            while(i < n) {
                if(s1[i] != s2[i]) {
                    return = false;
                    break;
                }
                i += 1;
            }
        }

        (inline, nochecks) bool strcmp(const(char)* s1, const(char)* s2) {
            char* _s1 = s1;
            char* _s2 = s2;
            return = false;
            while((_s1[0] != '\0') && (_s1[0] == _s2[0])) {
                _s1 = itop(char*, ptoi(_s1) + 1);
                _s2 = itop(char*, ptoi(_s2) + 1);
            }
            if(_s1[0] == _s2[0]) return = true;
        }

        (nochecks) char* strstr(const(char)* str, const(char)* substr) {
            int i = 0;
            char* ptr = str;
            bool done = false;

            while(ptr[i] != '\0') {
                if(std::cstring::strncmp(ptr, substr, std::cstring::strlen(substr))) {
                    done = true;
                    break;
                }
                ptr = itop(char*, ptoi(ptr) + 1);
            }

            if(!done) ptr = null;
        } => ptr;

        (nochecks) int indexOf(const(char)* str, char ch) {
            while((str[return] != '\0') && (str[return] != ch)) return += 1;
            if(str[return] == '\0') return = -1;
        }

        char* ltos(long number, char* endBuffer) {
            char[20] ltosBuffer;

            if(number == 0) {
                endBuffer[0] = '0';
                @return(endBuffer);
            }

            bool isNegative = false;
            if(number < 0) {
                isNegative = true;
                number = -number;
            }

            int index = 0;
            while(number > 0) {
                ltosBuffer[index] = ((number % 10l) + '0');
                number = number / 10;
                index += 1;
            }

            if(isNegative) {
                ltosBuffer[index] = '-';
                index += 1;
            }

            endBuffer[index] = '\0';

            int endI = 0;
            for(int i=index-1; i>=0; i--) {
                endBuffer[endI] = ltosBuffer[i];
                endI += 1;
            }
        } => endBuffer;

        char* ctos(cent number, char* endBuffer) {
            char[20] ctosBuffer;

            if(number == 0) {
                endBuffer[0] = '0';
                @return(endBuffer);
            }

            bool isNegative = false;
            if(number < 0) {
                isNegative = true;
                number = -number;
            }

            int index = 0;
            while(number > 0) {
                ctosBuffer[index] = ((number % cast(cent)10) + '0');
                number = number / 10;
                index += 1;
            }

            if(isNegative) {
                ctosBuffer[index] = '-';
                index += 1;
            }

            endBuffer[index] = '\0';

            int endI = 0;
            for(int i=index-1; i>=0; i--) {
                endBuffer[endI] = ctosBuffer[i];
                endI += 1;
            }
        } => endBuffer;
        
        char* dtos(double number, int precision, char* buffer) {
            long lPart;
            int i;
            int tI;
            int len;
            char[24] temp;
            int decimals;

            bool isNegative = false;
            if(number < 0.0d) {
                isNegative = true;
                number = -number;
                buffer[0] = '-';
                i += 1;
            }

            lPart = cast(long)number;
            std::cstring::ltos(lPart, &temp);
            len = std::cstring::strlen(&temp);
            while(tI < len) {
                buffer[i] = temp[tI];
                i += 1;
                tI += 1;
            }
            number = number - cast(double)lPart;
            buffer[i] = '.';
            i += 1;
            decimals = 0;
            while(decimals < precision) {
                number = number * 10;
                lPart = cast(long)number;
                buffer[i] = lPart + '0';
                i += 1;
                decimals += 1;
                number = number - cast(double)lPart;
            }
            buffer[i] = '\0';
        } => buffer;
    }
}
